plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = jSnakeGroupId
version = jSnakeVersion

configurations {
    shadow
    implementation.extendsFrom(shadow)
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.snaker.xyz/releases" }
}

dependencies {
    implementation("com.google.code.gson:gson:${gsonVersion}")
    compileOnly("org.jetbrains:annotations:${annotationsVersion}")
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar.enabled = false

build.dependsOn shadowJar

shadowJar {
    configurations = [project.configurations.shadow]

    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    setArchiveClassifier('')

    manifest {
        attributes 'FMLModType': 'GAMELIBRARY'
        attributes 'Automatic-Module-Name': group
    }

    from file('LICENSE')
    from sourceSets.main.output
}

publishing {
    repositories {
        maven {
            url "https://repo.snaker.xyz/releases"

            credentials {
                username System.getenv("MVN_USER")
                password System.getenv("MVN_PASS")
            }
        }
    }
    publications {
        register('pub', MavenPublication) {
            artifacts = [shadowJar, sourcesJar, javadocJar]

            pom {
                name = jSnakeArtifactId
                description = jSnakeDescription
                url = jSnakeVcsUrl

                scm {
                    url = jSnakeVcsUrl
                    connection = jSnakeVcsConnection
                    developerConnection = jSnakeVcsDevConnection
                }

                licenses {
                    license {
                        name = jSnakeLicense
                        url = jSnakeLicenseUrl
                    }
                }
            }
        }
    }
}