plugins {
    id 'java'
    id 'maven-publish'
}

group = jSnakeGroupId
version = jSnakeVersion

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.snaker.xyz/releases" }
}

dependencies {
    implementation("com.google.code.gson:gson:${gsonVersion}")
    compileOnly("org.jetbrains:annotations:${annotationsVersion}")
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    setArchiveClassifier('')

    manifest {
        attributes 'FMLModType': 'GAMELIBRARY'
        attributes 'Automatic-Module-Name': group
    }

    from file('LICENSE')
    from sourceSets.main.output
}

publishing {
    repositories {
        maven {
            url "https://repo.snaker.xyz/releases"

            credentials {
                username System.getenv("maven.username")
                password System.getenv("maven.password")
            }
        }
    }
    publications {
        register('pub', MavenPublication) {
            artifacts = [jar, sourcesJar, javadocJar]

            pom {
                name = jSnakeArtifactId
                description = jSnakeDescription
                url = jSnakeVcsUrl

                scm {
                    url = jSnakeVcsUrl
                    connection = jSnakeVcsConnection
                    developerConnection = jSnakeVcsDevConnection
                }

                licenses {
                    license {
                        name = jSnakeLicense
                        url = jSnakeLicenseUrl
                    }
                }
            }
        }
    }
}